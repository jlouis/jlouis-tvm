%% TAL Prelude.
%% Jesper Louis Andersen 2009.

%{

This defines the prelude of the TAL project. In here, we define a basis in the
same vein as the Haskell prelude or the Standard ML pervasives: A helper set of
commonly used constructions.

}%

%%%%%{ Reasoning from false }%%%%%


%{

To reason from false, we use the constant 'void' which is uninhabited. This is
in accordance with the Twelf naming conventions.

}%

void : type. %freeze void.




%%%%%{ Booleans }%%%%%

%{

A boolean value is either true or false. We define abbreviations for
these for convenience.

}%

bool : type.

bool/true : bool.
bool/false : bool.

%abbrev true = bool/true.
%abbrev false = bool/false.

%{

Booleans can be checked for equality and they admit negation. Also, they admit
identity by reflection.

}%

bool-eq : bool -> bool -> bool -> type.
%mode bool-eq +B1 +B2 -B3.

bool-eq/refl : bool-eq B B true.

bool-eq/true-false : bool-eq true false false.

bool-eq/false-true : bool-eq false true false.



bool-neg : bool -> bool -> type.
%mode bool-neg +B1 -B2.

bool-neg/true : bool-neg true false.

bool-neg/false : bool-neg false true.




%%%%%{ Natural numbers }%%%%%

%{

It is often convenient to have access to natural numbers, peano style. We call
the family 'nat' as per the usual convention.

For convenience, we define abbreviations on natural numbers so we can refer to
them as 'z' and 's N' respectively. Note that this variant defines them such
that they will retain their original name (nat/z and nat/s) in the Twelf output
buffer

}%

nat : type. %name nat N.

nat/z : nat.
nat/s : nat -> nat.

%abbrev z = nat/z.
%abbrev s = nat/s.

%{

Numbers can be added to each other by a simple inference rule
system. We call this operation 'nat-plus'. Similarly 'nat-mone'
defines the well-known mone operation on natural numbers.

The nat-lt operation defines a straightforward less-than (<)
operation.

}%

nat-plus : nat -> nat -> nat -> type.
%mode nat-plus +N1 +N2 -N3.

nat-plus/z : nat-plus z N N.

nat-plus/s : nat-plus (s N) K (s L)
	      <- nat-plus N K L.

nat-mone : nat -> nat -> nat -> type.
%mode nat-mone +N1 +N2 -N3.

nat-mone/z : nat-mone z N z.

nat-mone/s-1 : nat-mone (s N) z (s N).

nat-mone/s-2 : nat-mone (s N) (s L) K
		<- nat-mone N L K.


%{ Relations on natural numbers }%

nat-eq : nat -> nat -> type.

nat-eq/refl : nat-eq N N.


nat-neq : nat -> nat -> type. %name nat-neq NEQ.
%mode nat-neq +N1 +N2.

nat-neq/zs : nat-neq z (s _).
nat-neq/sz : nat-neq (s _) z.
nat-neq/ss : nat-neq (s K1) (s K2)
	      <- nat-neq K1 K2.


nat-lt : nat -> nat -> nat -> type.
%mode nat-lt +N1 +N2 -B.

nat-lt/z-z : nat-lt z z z.

nat-lt/z-s : nat-lt z (s _) (s z).

nat-lt/s-z : nat-lt (s _) z z.

nat-lt/s-s : nat-lt (s N) (s K) B
	      <- nat-lt N K B.



%{

Peano constants for convenience.

}%

0 = z.
1 = s 0.
2 = s 1.
3 = s 2.
4 = s 3.
5 = s 4.
6 = s 5.
7 = s 6.
8 = s 7.
9 = s 8.
10 = s 9.
11 = s 10.

