%{ Small playtoy language for
   working with function calls

   TODO: add F and Gamma!

}%

%{ ** Prelude ** }%
nat : type.

z : nat.
s : nat -> nat.

%{ ** SYNTAX ** }%

%% Function id's. These are just natural numbers
fid : type.
fid/ : nat -> type.

%% Terms
tm : type.

tm/skip : tm.
tm/semi : tm -> tm -> tm.
tm/fcall : fid -> tm.

%% Function definition lists
defs : type.

defs/z : defs.
defs/s : fid -> tm -> defs -> defs.

%% Programs
pgm : type.

pgm/ : defs -> tm -> pgm.

%{ ** Operational semantics ** }%

%% Function definition lookup.
def-lookup : defs -> fid -> tm -> type.
%mode def-lookup +D +F -T.

def-lookup/hit : def-lookup (defs/s F B _) F B.
def-lookup/miss : def-lookup (defs/s F' _ R) F Q
		   <- def-lookup R F Q.

%% Term evaluation
ev : defs -> tm -> tm -> type.

ev/skip : ev _ (tm/semi tm/skip T) T.
ev/semi : ev D (tm/semi T T2) (tm/semi T' T2)
	   <- ev Defs T T'.
ev/fcall : ev D (tm/fcall F) B
	    <- def-lookup D F B.

%% Program evaluation
ev-pgm : pgm -> pgm -> type.

ev-pgm/ : ev-pgm (pgm/ Defs T) (pgm/ Defs T')
	   <- ev Defs T T'.

%{ ** Type system ** }%

%% Helping type environment for functions
gamma : type.

gamma/z : gamma.
gamma/s : fid -> gamma -> gamma.

gamma-lookup : gamma -> fid -> type.
%mode gamma-lookup +G +F.

gamma-lookup/hit : gamma-lookup (gamma/s F _) F.
gamma-lookup/miss : gamma-lookup (gamma/s F' R) F
		     <- gamma-lookup R F.

%% Welformednesss relation. Defines when a term abides a Gamma-typing.
%%   notated as [ G |- t ]
wf : gamma -> tm -> type.
%mode wf +G +T.

wf/skip : wf _ tm/skip.
wf/semi : wf G (tm/semi T1 T2)
	   <- wf G T1
	   <- wf G T2.
wf/fcall : wf G (tm/fcall F)
	    <- gamma-lookup G F.

%% Wellformedness relation on definitions [ G |- d : G' ]
wf-def : gamma -> defs -> gamma -> type.
%mode wf-def +G +D +G'.

wf-def/z : wf-def G defs/z gamma/z.
wf-def/s : wf-def G (defs/s F T DR) (gamma/s F GR)
	    <- wf G T
	    <- wf-def G DR GR.

%% Wellformedness on programs [ |- P ]

wf-pgm : pgm -> type.

%% TODO: Perhaps we should derive Gamma explicitly. It is fairly easy.
wf-pgm/ : wf-pgm (pgm/ D T)
	   <- ({G : gamma} wf-def G D G)
	   <- ({G : gamma} wf G T).


