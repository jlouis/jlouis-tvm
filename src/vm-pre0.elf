%{

The VM pre0 code is an attempt at formalizing the simple basics of a functional
VM language


TODO list:

- Split the terms into an LLVM-like construction.

}%

%{

Syntax

}%

%{ Type system }%

tp : type. %name tp TP.

tp/nat : tp.
tp/bool : tp.

tp-list : type.

tp-list/z : tp-list. %name tp-list TPL.
tp-list/s : tp -> tp-list -> tp-list.

%{ Language syntax }%
tm : tp -> type. %name tm T.
%% Function names, with intrinsicly defined parameter and return types.
fname : tp-list -> tp -> type. %name fname F.
decls : tp -> type. %% Type of letrec body
defs : tp -> type. %% Type of letrec body.
fpar : tp-list -> tp -> type. %% Function parameters.
tm-list : tp-list -> type. %% List of terms

decls/z : defs T -> decls T.
decls/s : (fname TS T -> decls T) -> decls T.

defs/z : tm T -> defs T.
defs/s : fname TS T -> fpar TS T -> defs T' -> defs T'.

fpar/z : tm T -> fpar tp-list/z T.
fpar/s : (tm T -> fpar TS T') -> fpar (tp-list/s T TS) T'.



tm/nat : nat -> tm tp/nat.
tm/bool : nat -> tm tp/bool.
tm/plus : tm tp/nat -> tm tp/nat -> tm tp/nat.
tm/mone : tm tp/nat -> tm tp/nat -> tm tp/nat.
tm/ifte : tm tp/bool -> tm T -> tm T -> tm T.
tm/let : tm T -> (tm T -> tm T') -> tm T'.
tm/fcall : fname TS T -> tm-list TS -> tm T.
tm/letrec : decls T -> tm T.
tm/isz : tm tp/nat -> tm tp/bool.

nat    = tm/nat.
plus   = tm/plus.
mone   = tm/mone.
ifte   = tm/ifte.
let    = tm/let.
fcall  = tm/fcall.
letrec = tm/letrec.
isz    = tm/isz.


true = tm/bool 1.
false = tm/bool 0.

tm-list/z : tm-list tp-list/z.
tm-list/s : tm T -> tm-list TS -> tm-list (tp-list/s T TS).

nil = tm-list/z.
cons = tm-list/s.

%{ Static Semantics }%
value : tm T -> type.

value/nat : value (tm/nat _).



value-decls : decls T -> tm T -> type.
value-defs  : defs T -> tm T -> type.

value-decls/z : value-decls (decls/z D) T
		 <- value-defs D T.

value-decls/s : value-decls (decls/s [f : fname TS T] D f) T'
		 <- ({f : fname TS T} value-decls (D f) T').



value-defs/s : value-defs (defs/s _ _ D) T
		<- value-defs D T.

value-defs/z : value-defs (defs/z T) T
		<- value T.



value-list : tm-list TS -> type.

value-list/z : value-list tm-list/z.

value-list/s : value-list (tm-list/s V R)
		<- value V
		<- value-list R.

%{ Small-step relation }%

%% Mapping from function names into the body of the function.
func-map : fname TS T -> fpar TS T -> type.


%% Applying a function to its parameters.
func-apply : fpar TS T -> tm-list TS -> tm T -> type.


func-apply/z : func-apply (fpar/z T) tm-list/z T.

func-apply/s : func-apply (fpar/s [x] E x) (tm-list/s T R) T'
		<- func-apply (E T) R T'.

%% Evaluation context for terms.
pctx : (tm T -> tm T') -> type.

pctx/plus-1 : pctx ([x] tm/plus x _).

pctx/plus-2 : pctx ([x] tm/plus V x)
	       <- value V.

pctx/mone-1 : pctx ([x] tm/mone x _).

pctx/mone-2 : pctx ([x] tm/mone V x)
	       <- value V.

pctx/ifte   : pctx ([x] tm/ifte x _ _).

pctx/let : pctx ([x] tm/let x _).

pctx/isz : pctx ([x] tm/isz x).




step : tm T -> tm T -> type.
step-tm-list : tm-list TS -> tm-list TS -> type.

bind-decls : decls T -> decls T -> type.
bind-defs : defs T -> defs T -> type.



bind-decls/z : bind-decls (decls/z D) (decls/z D')
	    <- bind-defs D D'.

bind-decls/s : bind-decls (decls/s [f : fname TS T] D f) (decls/s [f : fname TS T] D' f)
	    <- ({f : fname TS T} bind-decls (D f) (D' f)).



bind-defs/z : bind-defs (defs/z T) (defs/z T')
	       <- step T T'.

bind-defs/s : bind-defs (defs/s F T D) (defs/s F T D')
	       <- (func-map F T -> bind-defs D D').



step/pctx : step (EC E) (EC E')
	     <- pctx EC
	     <- step E E'.

%%step/nat : step (tm/nat N) (tm/nat N).

step/plus : step (tm/plus (tm/nat N1) (tm/nat N2)) (tm/nat N3)
	     <- nat-plus N1 N2 N3.


step/mone : step (tm/mone (tm/nat N1) (tm/nat N2)) (tm/nat N3)
	     <- nat-mone N1 N2 N3.


step/if-true : step (tm/ifte (tm/bool (s _)) E1 E2) E1.

step/if-false : step (tm/ifte (tm/bool z) E1 E2) E2.

step/let : step (tm/let V B) (B V)
	    <- value V.

step/isz-t : step (tm/isz (tm/nat 0)) (tm/bool 1).
step/isz-f : step (tm/isz (tm/nat (s _))) (tm/bool 0).

step/fcall-apply : step (tm/fcall FN VS) E
		    <- value-list VS
		    <- func-map FN B
		    <- func-apply B VS E.

step/fcall-eval : step (tm/fcall FN TS) (tm/fcall FN TS')
		   <- step-tm-list TS TS'.

step/letrec-1 : step (tm/letrec B) V
		 <- value-decls B V.

step/letrec-2 : step (tm/letrec B) (tm/letrec B')
		 <- bind-decls B B'.





step-tm-list/z : step-tm-list tm-list/z tm-list/z.

step-tm-list/s-1 : step-tm-list (tm-list/s T R) (tm-list/s T' R)
		    <- step T T'.

step-tm-list/s-2 : step-tm-list (tm-list/s V R) (tm-list/s V R')
		    <- value V
		    <- step-tm-list R R'.

%{

Iterated step relations

}%

steps : tm T -> tm T -> type. %name steps Dsteps.

steps/0 : steps T T.

steps/s : steps T T''
	   <- step T T'
	   <- steps T' T''.


steps-to : tm T -> tm T -> type. %name steps-to Dsteps-to.

steps-to/i : steps-to T V
	      <- steps T V
	      <- value V.

%{

Query tests of the system

}%

ifte-test1 = (ifte (tm/bool 0) (tm/nat 1) (tm/nat 2)).
ifte-test2 = (ifte (tm/bool 3) (tm/nat 1) (tm/nat 2)).

%query 1 1 (step ifte-test1 (tm/nat 2)).
%query 1 1 (step ifte-test2 (tm/nat 1)).

gauss = [n] (letrec (decls/s [g : fname (tp-list/s tp/nat tp-list/z) tp/nat] decls/z
		      (defs/s g (fpar/s [x]
				  (fpar/z (ifte (isz x)
					     (tm/nat 0)
					     (plus
						x
						(fcall g (cons (mone x (tm/nat 1)) nil))))))
			 (defs/z (fcall g (cons n nil)))))).

%query 1 1 (steps-to (gauss (tm/nat 3)) (tm/nat 6)).

even = [n] (letrec (decls/s [even : fname (tp-list/s tp/nat tp-list/z) tp/nat]
		       decls/s [odd  : fname (tp-list/s tp/nat tp-list/z) tp/nat]
			 (decls/z
			    (defs/s even (fpar/s [x]
					    (fpar/z (ifte (isz x)
						       (tm/nat 1)
						       (fcall odd (cons (mone x (tm/nat 1)) nil)))))
			    (defs/s odd (fpar/s [x]
					   (fpar/z (ifte (isz (mone x (tm/nat 1)))
						      (tm/nat 0)
						      (fcall even (cons (mone x (tm/nat 1)) nil)))))
			    (defs/z (fcall even (cons n nil)))))))).

%query 1 1 (steps-to (even (tm/nat 0)) (tm/nat 1)).
%query 1 1 (steps-to (even (tm/nat 1)) (tm/nat 0)).
%query 1 1 (steps-to (even (tm/nat 2)) (tm/nat 1)).
%query 1 1 (steps-to (even (tm/nat 3)) (tm/nat 0)).

