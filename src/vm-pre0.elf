%{

The VM pre0 code is an attempt at formalizing the simple basics of a functional
VM language


TODO list:

- Trace execution of the infinite loop and find the problem.
- Rewrite Decls to contain a typing relation and check it
  such that each declaration has EXACTLY one definition.

}%

%{

Syntax

}%

%{ Type system }%

tp : type. %name tp TP.

tp/nat : tp.
tp/bool : tp.

tp-list : type.

tp-list/z : tp-list. %name tp-list TPL.
tp-list/s : tp -> tp-list -> tp-list.

%{ Language syntax }%
tm : tp -> type. %name tm T.
%% Function names, with intrinsicly defined parameter and return types.
fname : tp-list -> tp -> type. %name fname F.

fname-list : type.

fname-list/z : fname-list.
fname-list/s : fname TS T -> fname-list -> fname-list.

decls+defs+body : tp -> type.
defs : type. %% Type of letrec body.
fpar : tp-list -> tp -> type. %% Function parameters.
tm-list : tp-list -> type. %% List of terms

decls+defs+body/z : defs -> tm T -> decls+defs+body T.
decls+defs+body/s : (fname TS T -> decls+defs+body T) -> decls+defs+body T.

defs/z : defs.
defs/s : defs -> fpar TS T -> defs.

fpar/z : tm T -> fpar tp-list/z T.
fpar/s : (tm T -> fpar TS T') -> fpar (tp-list/s T TS) T'.

tm/nat : nat -> tm tp/nat.
tm/bool : nat -> tm tp/bool.
tm/plus : tm tp/nat -> tm tp/nat -> tm tp/nat.
tm/mone : tm tp/nat -> tm tp/nat -> tm tp/nat.
tm/ifte : tm tp/bool -> tm T -> tm T -> tm T.
tm/let : tm T -> (tm T -> tm T') -> tm T'.
tm/fcall : fname TS T -> tm-list TS -> tm T.
tm/letrec : decls+defs+body T -> tm T.
tm/isz : tm tp/nat -> tm tp/bool.

nat    = tm/nat.
plus   = tm/plus.
mone   = tm/mone.
ifte   = tm/ifte.
let    = tm/let.
fcall  = tm/fcall.
letrec = tm/letrec.
isz    = tm/isz.


true = tm/bool 1.
false = tm/bool 0.

tm-list/z : tm-list tp-list/z.
tm-list/s : tm T -> tm-list TS -> tm-list (tp-list/s T TS).

nil = tm-list/z.
cons = tm-list/s.


%{ Static Semantics }%
value : tm T -> type.
%mode value +T.

value/nat : value (tm/nat _).

value-decls+defs+body : decls+defs+body T -> tm T -> type.
%mode value-decls+defs+body +DDB -T.

value-decls+defs+body/z : value-decls+defs+body (decls+defs+body/z _ T) T
			   <- value T.

value-decls+defs+body/s : value-decls+defs+body (decls+defs+body/s [f] D f) T
			   <- ({f} value-decls+defs+body (D f) T).


%{ Small-step relation }%

%% Mapping from function names into the body of the function.
func-map : fname TS T -> fpar TS T -> type.
%mode func-map +F -FP.

%% Applying a function to its parameters.
func-apply : fpar TS T -> tm-list TS -> tm T -> type.
%mode func-apply +FP +TML -T.

func-apply/z : func-apply (fpar/z T) tm-list/z T.

func-apply/s : func-apply (fpar/s [x] E x) (tm-list/s T R) T'
		<- func-apply (E T) R T'.

%% Evaluation context for terms.
step : tm T -> tm T -> type.
%mode step +T -T'.

%% step underneath a decls+defs+body construction. This part unrolls the names unto a stack
step-decls : decls+defs+body T -> fname-list -> decls+defs+body T -> type.
%mode step-decls +DDB +FL -DDB'.

%% Bind definitions to functions and step the body underneath it
step-defs : fname-list -> defs -> tm T -> tm T -> type.
%mode step-defs +FL +Defs +Tm -Tm'.



step-decls/s : step-decls (decls+defs+body/s [f] D f) FL (decls+defs+body/s [f] D' f)
		<- ({f} step-decls (D f) (fname-list/s f FL) (D' f)).

step-decls/z : step-decls (decls+defs+body/z Defs Tm) FL (decls+defs+body/z Defs Tm')
		<- step-defs FL Defs Tm Tm'.






step-defs/s : step-defs (fname-list/s F FL) (defs/s DR FB) T T'
	       <- (func-map F FB -> step-defs FL DR T T').

step-defs/z : step-defs fname-list/z defs/z T T'
	       <- step T T'.





step/isz : step (tm/isz E) (tm/isz E')
	    <- step E E'.

step/let : step (tm/let E B) (tm/let E' B)
	    <- step E E'.

step/ifte-tst : step (tm/ifte E K1 K2) (tm/ifte E' K1 K2)
		 <- step E E'.

step/plus-1 : step (tm/plus E K) (tm/plus E' K)
	       <- step E E'.

step/plus-2 : step (tm/plus V E) (tm/plus V E')
	       <- value V
	       <- step E E'.

step/mone-1 : step (tm/mone E K) (tm/mone E' K)
	       <- step E E'.

step/mone-2 : step (tm/mone V E) (tm/mone V E')
	       <- value V
	       <- step E E'.

step/plus : step (tm/plus (tm/nat N1) (tm/nat N2)) (tm/nat N3)
	     <- nat-plus N1 N2 N3.


step/mone : step (tm/mone (tm/nat N1) (tm/nat N2)) (tm/nat N3)
	     <- nat-mone N1 N2 N3.


step/if-true : step (tm/ifte (tm/bool (s _)) E1 E2) E1.

step/if-false : step (tm/ifte (tm/bool z) E1 E2) E2.

step/let : step (tm/let V B) (B V)
	    <- value V.

step/isz-t : step (tm/isz (tm/nat 0)) (tm/bool 1).
step/isz-f : step (tm/isz (tm/nat (s _))) (tm/bool 0).

step/fcall-apply : step (tm/fcall FN VS) E
		    <- func-map FN B
		    <- func-apply B VS E.

step/letrec-1 : step (tm/letrec B) V
		 <- value-decls+defs+body B V.

step/letrec-2 : step (tm/letrec B) (tm/letrec B')
		 <- step-decls B fname-list/z B'.


%{

Iterated step relations

}%

steps : tm T -> tm T -> type. %name steps Dsteps.
%mode steps +T -T'.

steps/0 : steps T T.

steps/s : steps T T''
	   <- step T T'
	   <- steps T' T''.


steps-to : tm T -> tm T -> type. %name steps-to Dsteps-to.
%mode steps-to +T -V.

steps-to/i : steps-to T V
	      <- steps T V
	      <- value V.

%{

Query tests of the system

}%


%% Test if-then-else
ifte-test1 = (ifte (tm/bool 0) (tm/nat 1) (tm/nat 2)).
ifte-test2 = (ifte (tm/bool 3) (tm/nat 1) (tm/nat 2)).

%query 1 1 (step ifte-test1 (tm/nat 2)).
%query 1 1 (step ifte-test2 (tm/nat 1)).

%% Test isz
%query 1 1 (step (isz (tm/nat 3)) false).
%query 0 1 (step (isz (tm/nat 3)) true).
%query 1 1 (step (isz (tm/nat 0)) true).
%query 0 1 (step (isz (tm/nat 0)) false).

%query 1 1 (step (mone (tm/nat 3) (tm/nat 1)) (tm/nat 2)).

letrec-test1 = letrec (decls+defs+body/z defs/z (tm/nat 0)).
letrec-test2 = letrec (decls+defs+body/z defs/z (plus (tm/nat 1) (tm/nat 2))).
letrec-test3 = letrec (decls+defs+body/s [g : fname (tp-list/s tp/nat tp-list/z) tp/nat]
			 (decls+defs+body/z (defs/s defs/z
					       (fpar/s [x]
						  (fpar/z (plus x (tm/nat 1)))))
			    (fcall g (cons (tm/nat 2) nil)))).
%% 3rd test is to include something which we call.

%query 1 2 (steps-to letrec-test1 (tm/nat 0)).
%query 1 2 (steps-to letrec-test2 (tm/nat 3)).
%query 0 3 (steps-to letrec-test2 (tm/nat 0)).
%query 1 2 (steps-to letrec-test3 X).

gauss = [n] (letrec (decls+defs+body/s [g : fname (tp-list/s tp/nat tp-list/z) tp/nat]
		       (decls+defs+body/z
			  (defs/s defs/z (fpar/s [x]
					    (fpar/z (ifte (isz x)
						       (tm/nat 0)
						       (plus
							  x
							  (fcall g (cons (mone x (tm/nat 1)) nil)))))))
			  (fcall g (cons n nil))))).

%query 1 5 (steps-to (gauss (tm/nat 0)) X).
%query 0 1 (steps-to (gauss (tm/nat 3)) (tm/nat 1)).

even = [n] (letrec (decls+defs+body/s [even : fname (tp-list/s tp/nat tp-list/z) tp/nat]
		       decls+defs+body/s [odd  : fname (tp-list/s tp/nat tp-list/z) tp/nat]
			 (decls+defs+body/z
			    (defs/s
			       (defs/s defs/z
				  (fpar/s [x]
				     (fpar/z (ifte (isz x)
						(tm/nat 1)
						(fcall odd (cons (mone x (tm/nat 1)) nil))))))
			       (fpar/s [x]
				  (fpar/z (ifte (isz x)
					     (tm/nat 0)
					     (fcall even (cons (mone x (tm/nat 1)) nil))))))
			    (fcall even (cons n nil))))).

%query 1 2 (steps-to (even (tm/nat 0)) (tm/nat 1)).
%query 1 2 (steps-to (even (tm/nat 1)) (tm/nat 0)).
%query 1 2 (steps-to (even (tm/nat 2)) (tm/nat 1)).
%query 1 1 (steps-to (even (tm/nat 3)) (tm/nat 0)).



