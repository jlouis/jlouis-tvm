%{

******** Type System ********

}%

%% Primitive types
tp : type. %name tp Tp.

tp/nat : tp.
tp/bool : tp.

%% Lists of primitive types
tp-list : type. %name tp-list TpL.

tp-list/nil : tp-list.
tp-list/cons : tp -> tp-list -> tp-list.

%% Function types
fun-tp : type. %name fun-tp FTp.
%% There is only way to introduce a function type.
fun-tp/ : tp-list -> tp -> fun-tp.

fun-tp-list : type. %name fun-tp-list FTpL.
fun-tp-list/nil : fun-tp-list.
fun-tp-list/cons : fun-tp -> fun-tp-list -> fun-tp-list.

%{

******** Syntax ********

}%

%{

Registers is a family of either constants or variables. But as variables
are captured by HOAS, we do not need to specify them.

Expressions are either constants, additions or less-than comparisons
in this silly language.

}%

reg : tp -> type. %name reg R r.

reg/cst-n : nat -> reg tp/nat.
reg/cst-b : nat -> reg tp/bool.

%{

We will need lists of registers when one makes function calls and when one
jumps to other basic blocks. Hence, we define them here

}%

reg-list : tp-list -> type. %name reg-list RL.

reg-list/nil : reg-list tp-list/nil.
reg-list/cons : reg T -> reg-list TL -> reg-list (tp-list/cons T TL).

%{ Introduction of function bundles.

   Function names
   Function bundles

   TODO: DOCUMENT FOR GODS SAKE!
}%

fname : fun-tp -> type. %name fname Fn.
fbundle : fun-tp-list -> type.
fbundle+ : fun-tp-list -> type.

%{ This constant only exists to appease Twelfs splitter in the input coverage }%
fbundle0 : fbundle fun-tp-list/nil.

f-hd : fbundle+ (fun-tp-list/cons FT FTL) -> fname FT.
f-tl/z : fbundle FTL -> fbundle+ FTL.
f-tl/s : fbundle+ (fun-tp-list/cons FT FTL) -> fbundle+ FTL.



op : tp -> type.
op-list : tp-list -> type.

op-list/nil : op-list tp-list/nil.
op-list/cons : op T -> op-list TL -> op-list (tp-list/cons T TL).

%{ Syntactic term equality }%
op-eq : op T -> op T -> type.

op-eq/refl : op-eq T T.



fun-def : fun-tp -> type.

fun-def/body : op T -> (fun-def (fun-tp/ tp-list/nil T)).
fun-def/parm : (reg T0 -> fun-def (fun-tp/ TL T)) -> fun-def (fun-tp/ (tp-list/cons T0 TL) T).

fun-def-list : fun-tp-list -> type.

fun-def-list/nil : fun-def-list fun-tp-list/nil.
fun-def-list/cons : fun-def FT -> fun-def-list FTL -> fun-def-list (fun-tp-list/cons FT FTL).

fun-list : fun-tp-list -> type.

fun-list/nil : fun-list fun-tp-list/nil.
fun-list/cons : fun-def FT -> fun-list FTL -> fun-list (fun-tp-list/cons FT FTL).


%{ Primitive constants }%
op/reg : reg T -> op T.

%{ Operations on registers }%
op/plus  : reg tp/nat -> reg tp/nat -> op tp/nat.
op/cmp-lt : reg tp/nat -> reg tp/nat -> op tp/bool.

insn : tp -> type.
%{ Binding }%
insn/return : reg T -> insn T.
insn/br : fname (fun-tp/ TL T) -> reg-list TL -> insn T.
tm/brc : reg tp/bool -> fname (fun-tp/ TL T) -> reg-list TL
                     -> fname (fun-tp/ TL1 T) -> reg-list TL1 -> insn T.

insn/let : op T1 -> (reg T1 -> insn T) -> insn T.
tm/letrec : (fbundle FTL -> fun-def-list FTL)
               -> (fbundle FTL -> insn T) -> insn T.


