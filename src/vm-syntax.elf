%{

******** Type System ********

}%

%% Primitive types
tp : type. %name tp Tp.

tp/nat : tp.
tp/bool : tp.

%% Lists of primitive types
tp-list : type. %name tp-list TpL.

tp-list/nil : tp-list.
tp-list/cons : tp -> tp-list -> tp-list.

%% Basic-block types
bb-tp : type. %name bb-tp BBTp.
%% There is only way to introduce a function type.
bb-tp/ : tp-list -> tp -> bb-tp.

bb-tp-list : type. %name bb-tp-list BBTpL.
bb-tp-list/nil : bb-tp-list.
bb-tp-list/cons : bb-tp -> bb-tp-list -> bb-tp-list.

%{

******** Syntax ********

}%

%{

Registers is a family of either constants or variables. But as variables
are captured by HOAS, we do not need to specify them.

Expressions are either constants, additions or less-than comparisons
in this silly language.

}%

cst : tp -> type. %name cst R r.

cst/cst-n : nat -> cst tp/nat.
cst/cst-b : nat -> cst tp/bool.

%{

We will need lists of registers when one makes function calls and when one
jumps to other basic blocks. Hence, we define them here

}%

cst-list : tp-list -> type. %name cst-list RL.

cst-list/nil : cst-list tp-list/nil.
cst-list/cons : cst T -> cst-list TL -> cst-list (tp-list/cons T TL).

%{ Introduction of function bundles.

   Function names
   Function bundles

   TODO: DOCUMENT FOR GODS SAKE!
}%

fname : bb-tp -> type. %name fname Fn.
pbundle : bb-tp-list -> type.
pbundle+ : bb-tp-list -> type.

%{ This constant only exists to appease Twelfs splitter in the input coverage }%
pbundle0 : pbundle bb-tp-list/nil.

f-hd : pbundle+ (bb-tp-list/cons FT FTL) -> fname FT.
f-tl/z : pbundle FTL -> pbundle+ FTL.
f-tl/s : pbundle+ (bb-tp-list/cons FT FTL) -> pbundle+ FTL.



op : tp -> type.
%{ Instructions. Either assigning to a register, a return or a branch.
   These take care of control flow }%
insn : tp -> type.

op-list : tp-list -> type.

op-list/nil : op-list tp-list/nil.
op-list/cons : op T -> op-list TL -> op-list (tp-list/cons T TL).

%{ Syntactic term equality }%
op-eq : op T -> op T -> type.

op-eq/refl : op-eq T T.


fun-id : tp -> tp-list -> type.

fun-id/ : nat -> fun-id T TL.


bb-def : bb-tp -> type.

bb-def/body : insn T -> (bb-def (bb-tp/ tp-list/nil T)).
bb-def/phi : (cst T0 -> bb-def (bb-tp/ TL T)) -> bb-def (bb-tp/ (tp-list/cons T0 TL) T).

bb-def-list : bb-tp-list -> type.

bb-def-list/nil : bb-def-list bb-tp-list/nil.
bb-def-list/cons : bb-def FT -> bb-def-list FTL -> bb-def-list (bb-tp-list/cons FT FTL).

bb-list : bb-tp-list -> type.

bb-list/nil : bb-list bb-tp-list/nil.
bb-list/cons : bb-def FT -> bb-list FTL -> bb-list (bb-tp-list/cons FT FTL).


%{ Operations. These are simple operations returning a value to a cstister }%
%{ Primitive constants }%
op/cst : cst T -> op T.

%{ Operations on registers }%
op/plus   : cst tp/nat -> cst tp/nat -> op tp/nat.
op/cmp-lt : cst tp/nat -> cst tp/nat -> op tp/bool.
op/mone   : cst tp/nat -> cst tp/nat -> op tp/nat.

%{ Binding }%
insn/return : cst T -> insn T.

insn/br : fname (bb-tp/ TL T) -> cst-list TL -> insn T.
insn/brc : cst tp/bool -> fname (bb-tp/ TL T) -> cst-list TL
                     -> fname (bb-tp/ TL1 T) -> cst-list TL1 -> insn T.
insn/let : op T1 -> (cst T1 -> insn T) -> insn T.
insn/letrec : (pbundle FTL -> bb-def-list FTL)
               -> (pbundle FTL -> insn T) -> insn T.
insn/do : insn T' -> (cst T' -> insn T) -> insn T. %% Only internally used
insn/call : fun-id T TL -> cst-list TL -> (cst T -> insn T) -> insn T.

%{ **** Functions **** }%

%% TODO: Should functions bear static information about their types?
fun-decl : tp -> tp-list -> type.

fun-decl/parm : (cst T -> fun-decl R TL) -> fun-decl R (tp-list/cons T TL).
fun-decl/body : insn T -> fun-decl T tp-list/nil.


defs : type.

defs/z : defs.
defs/s : fun-id T TL -> fun-decl T TL -> defs -> defs.

pgm : type.
pgm/ : defs -> insn T -> pgm.


