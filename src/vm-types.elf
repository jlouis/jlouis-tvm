%{

  Extrinsic type system for the functions

}%

%{ Gamma is the typing environment }%
gamma : type.

gamma/z : gamma.
gamma/s : fun-id T TL -> gamma -> gamma.

gamma-lookup : gamma -> fun-id T TL -> type.
%mode gamma-lookup +G +F.

gamma-lookup/hit : gamma-lookup (gamma/s F _) F.

gamma-lookup/miss : gamma-lookup (gamma/s F' R) F
		     <- gamma-lookup R F.

%{ F \notin D -- says that F is not present as a function name in D }%
f-notin : fun-id T TL -> defs -> type.

f-notin/z : f-notin F defs/z.
f-notin/s : f-notin (fun-id/ N1) (defs/s (fun-id/ N2) _ R)
	     <- nat-neq N1 N2
	     <- f-notin (fun-id/ N1) R.

%{ Wellformedness of terms [ G |- t ] }%
wf : gamma -> insn T -> type.

%{ Wellformedness of basic blocks [ G |- bb ] }%
wf-bb : gamma -> bb-def T -> type.

wf-bb/phi  : wf-bb G (bb-def/phi PHI)
	      <- ({r} wf-bb G (PHI r)).
wf-bb/body : wf-bb G (bb-def/body Insn)
	      <- wf G Insn.

%{ Wellformedness of pbundles [ G |- pb ] }%
wf-pb : gamma -> bb-def-list FTL -> type.

wf-pb/nil  : wf-pb G bb-def-list/nil.
wf-pb/cons : wf-pb G (bb-def-list/cons BBD R)
	      <- wf-bb G BBD
	      <- wf-pb G R.


wf/return : wf G (insn/return R).
wf/br     : wf G (insn/br _ _).
wf/brc    : wf G (insn/brc _ _ _ _ _).
wf/let    : wf G (insn/let Op Bdy)
	     <- ({r : reg T} wf G (Bdy r)).
wf/letrec : wf G (insn/letrec Defs Bdy)
	     <- ({pb : pbundle FTL} wf-pb G (Defs pb))
	     <- ({pb : pbundle FTL} wf G (Bdy pb)).
wf/do     : wf G (insn/do I Bdy)
	     <- wf G I
	     <- ({r : reg T} wf G (Bdy r)).

wf/call   : wf G (insn/call F RL Bdy)
	     <- gamma-lookup G F
	     <- ({r} wf G (Bdy r)).

wf-decl : gamma -> fun-decl R TL -> type.

wf-decl/parm : wf-decl G (fun-decl/parm [r : reg T] FD r)
		<- ({r : reg T} wf-decl G (FD r)).

wf-decl/body : wf-decl G (fun-decl/body (I : insn Tp))
		<- wf G I.


%{ Wellformedness of definitions [ G |- D : G' ] }%
wf-defs : gamma -> defs -> gamma -> type.

wf-defs/z : wf-defs G defs/z gamma/z.

wf-defs/s : wf-defs G (defs/s (F : fun-id T TL) (Decl : fun-decl T TL) DR) (gamma/s F GR)
	     <- wf-decl G Decl
	     <- f-notin F DR
	     <- wf-defs G DR GR.

wf-pgm : pgm -> type.

wf-pgm/ : wf-pgm (pgm/ D T)
	   <- wf G T
	   <- wf-defs G D G.
