%{

  Extrinsic type system for the functions

}%

%{ Gamma is the typing environment }%
gamma : type.

gamma/z : gamma.
gamma/s : fid -> tp-list -> tp -> gamma -> gamma.

gamma-lookup : gamma -> fid -> tp-list -> tp -> type.
%mode gamma-lookup +G +F -TL -T.

gamma-lookup/hit : gamma-lookup (gamma/s F TL T _) F TL T.

gamma-lookup/miss : gamma-lookup (gamma/s F' _ _ R) F TL T
		     <- gamma-lookup R F TL T.

%{ F \notin D -- says that F is not present as a function name in D }%
f-notin : fid -> defs -> type.
%mode f-notin +F +D.

f-notin/z : f-notin F defs/z.
f-notin/s : f-notin (fid/ N1) (defs/s (fid/ N2) _ R)
	     <- nat-neq N1 N2
	     <- f-notin (fid/ N1) R.

%{ Wellformedness of terms [ G |- t ] }%
wf : gamma -> insn T -> type.
%mode wf +G +Insns.
%{ Wellformedness of basic blocks [ G |- bb ] }%
wf-bb : gamma -> bb-def T -> type.
%mode wf-bb +G +BBD.

wf-bb/phi  : wf-bb G (bb-def/phi PHI)
	      <- ({r} wf-bb G (PHI r)).
wf-bb/body : wf-bb G (bb-def/body Insn)
	      <- wf G Insn.

%{ Wellformedness of pbundles [ G |- pb ] }%
wf-pb : gamma -> bb-def-list FTL -> type.
%mode wf-pb +G +BBD.

wf-pb/nil  : wf-pb G bb-def-list/nil.
wf-pb/cons : wf-pb G (bb-def-list/cons BBD R)
	      <- wf-bb G BBD
	      <- wf-pb G R.


wf/return : wf G (insn/return R).
wf/br     : wf G (insn/br _ _).
wf/brc    : wf G (insn/brc _ _ _ _ _).
wf/let    : wf G (insn/let Op Bdy)
	     <- ({r} wf G (Bdy r)).
wf/letrec : wf G (insn/letrec Defs Bdy)
	     <- ({pb : pbundle FTL} wf-pb G (Defs pb))
	     <- ({pb : pbundle FTL} wf G (Bdy pb)).
wf/do     : wf G (insn/do I Bdy)
	     <- wf G I
	     <- ({r} wf G (Bdy r)).
%{
wf/call   : wf G (insn/call ((fun-id/ N) : fun-id T TL) _ Bdy)
	     <- gamma-lookup G (fid/ N) TL T
	     <- ({r} wf G (Bdy r)).
}%

wf-decl : gamma -> fun-decl -> tp-list -> tp -> type.
%mode wf-decl +G +FD +TL +T.

wf-decl/parm : wf-decl G (fun-decl/parm [r : reg T] FD r) (tp-list/cons T TR) Tp
		<- ({r : reg T} wf-decl G (FD r) TR Tp).
wf-decl/body : wf-decl G (fun-decl/body (I : insn Tp)) tp-list/nil Tp
		<- wf G I.


%{ Wellformedness of definitions [ G |- D : G' ] }%
wf-defs : gamma -> defs -> gamma -> type.
%mode wf-defs +G +D +G'.

wf-defs/z : wf-defs G defs/z gamma/z.

wf-defs/s : wf-defs G (defs/s F Decl DR) (gamma/s F TL T GR)
	     <- wf-decl G Decl TL T
	     <- f-notin F DR
	     <- wf-defs G DR GR.

wf-pgm : pgm -> type.

wf-pgm/ : wf-pgm (pgm/ D T)
	   <- wf G T
	   <- wf-defs G D G.
