%{

  Extrinsic type system for the functions

}%

fun-tp-list : type.

fun-tp-list/nil : fun-tp-list.
fun-tp-list/cons : nat -> tp -> tp-list -> fun-tp-list -> fun-tp-list.


fun-tp-list-lookup : nat -> fun-tp-list -> tp -> tp-list -> type.
%mode fun-tp-list-lookup +N +FTL -T -TL.

fun-tp-list-lookup/hit : fun-tp-list-lookup N (fun-tp-list/cons N T TL _) T TL.
fun-tp-list-lookup/miss : fun-tp-list-lookup N (fun-tp-list/cons _ _ _ R) T TL
			   <- fun-tp-list-lookup N R T TL.

fun-discover-type : fun-list -> fun-tp-list -> type.

fun-discover-type/main : fun-discover-type (fun-list/main _) fun-tp-list/nil.
fun-discover-type/cons : fun-discover-type
			    (fun-list/cons (fun-id/ N) (_ : fun-decl R TL) Rest)
			    (fun-tp-list/cons N R TL Q)
			  <- fun-discover-type Rest Q.

fun-check-insn : fun-tp-list -> insn T -> type.

fun-check-insn/return : fun-check-insn TPL (insn/return _).

fun-check-insn/br     : fun-check-insn TPL (insn/br _ _).

fun-check-insn/brc    : fun-check-insn TPL (insn/brc _ _ _ _ _).

fun-check-insn/do     : fun-check-insn TPL (insn/do C ([r] C1 r))
			 <- fun-check-insn TPL C
			 <- ({r} fun-check-insn TPL (C1 r)).

fun-check-insn/let    : fun-check-insn TPL (insn/let _ ([r] C r))
			 <- ({r} fun-check-insn TPL (C r)).

%{
   TODO: Missing
fun-check-insn/letrec : fun-check-insn TPL (insn/letrec ([pb] ...
}%

fun-check-insn/call   : fun-check-insn TPL (insn/call (fun-id/ N)
					              (_ : reg-list TL)
					              ([r : reg T] I r))
			 <- fun-tp-list-lookup N TPL T TL.


fun-check-decl : fun-tp-list -> fun-decl R TL -> type.

fun-check-decl/parm : fun-check-decl TL (fun-decl/parm ([x] FD x))
		       <- ({x} fun-check-decl TL (FD x)).

fun-check-decl/body : fun-check-decl TL (fun-decl/body Insn)
		       <- fun-check-insn TL Insn.


fun-check-type : fun-tp-list -> fun-list -> type.

fun-check-type/main : fun-check-type _ (fun-list/main _).
fun-check-type/cons : fun-check-type TL (fun-list/cons (fun-id/ N)
					               (FDecl : fun-decl R RL)
					               Rest)
		       <- fun-tp-list-lookup N TL R RL
		       <- fun-check-decl TL FDecl
		       <- fun-check-type TL Rest.



check : fun-list -> type.

check/ : check FL
	  <- fun-discover-type FL TL
	  <- fun-check-type TL FL.

