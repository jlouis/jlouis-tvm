%% Definition of a LLVM-inspired Virtual Machine language
%% Jesper Louis Andersen, 2009.

%{

GLOBAL TODO:

- Implement the altered code from Appel's paper.
  TEST
- Begin constructing a type system with nats (i32)
  and booleans (i1).

}%

%{

>>> TODO
This is a list of observations made while defining this part of the
language.

* It looks as if the LLVM IR defines expressions as

exp : tp -> type.

rather than

exp : type.

This is NICE. I hoped for being able to define expressions as
type-dependent. Now it looks as if it is possible. At least it has to
be tried. Preservation for free in expressions are always a nice thing
to have.
<<<

* The two main desirable properties for this language is that of
preservation and progress w.r.t. types.

}%





%%%%%{ Maps and label identifiers }%%%%%

%{

A label is an identifier in the LLVM IR designating a label. Our encoding
of these is simple that a label carries a number identifying it. This creates
a supply of an (countably) infinite amount of labels for us to use.

}%

label : type.

label/n : nat -> label.

l_1 = label/n 1.
l_2 = label/n 2.
l_3 = label/n 3.
l_4 = label/n 4.



%{

In this part of the file, we define some mappings so we can store
things in the context. We hope this will make the development easier
later on.

We need:

A mapping from labels into basic blocks.

>>> NOTE
Perhaps we need more, but this is a start.
<<<

A 'bb' designates a basic block. We will define their contents later on.

}%


bb : type.
bb-map : label -> bb -> type.




%{

The LLVM IR is a linear instruction set. Code in a function is split
into basic blocks. A Basic block is defined as:

label:
  %i1 = phi(...);
  ...
  %in = phi(...);

  insn_1;
  ...
  insn_n;

  return/br;

That is, a basic block, is a label, followed by
eventual phi-nodes, followed by instructions linearly until we reach
either a br or a function return. Then we either proceed along
another basic block or return from the function in which the basic
block is defined.

This representation is easy to map into a graph-representation [Appel-SSA],
but it is not an optimal representation for an inference rule system,
nor Twelf. We want to utilize the ideas in [Appel-SSA] to rewrite this
system into one which is amenable for definition in Twelf.

>>> GOAL
Our goal is to get a way to define the Example from [Appel] in our
small toy-language here and use it to structure a skeleton for further
work.
<<<

>>> TODO
We do not explain anything about phi-nodes yet. We ought to.
<<<

Suppose we only have assignment instructions of the form

%x = exp;

where 'exp' is either a constant, or of the form %x + %y for variables
x and y. For simplicity we will let the constants be natural numbers
for the moment.

}%






%%%%%{ Expression Syntax }%%%%%

%{

Registers is a family of either constants or variables. But as variables
are captured by HOAS, we do not need to specify them.

Expressions are either additions or less-than comparisons in this silly
language.

}%

reg : type. %name reg R.

reg/constant : nat -> reg.

exp : type. %name exp E.

exp/constant : reg -> exp.
exp/plus : reg -> reg -> exp.
exp/lt : reg -> reg -> exp.

%% For convenience.
constant = [n] (exp/constant (reg/constant n)).

%{

>>> NOTE
We stratify into 'reg' and 'exp' respectively to eliminate arbitrarily
nested expression trees. In the LLVM IR we only have primitive operations
or [constant expressions] which are expression trees that can be constant
folded. They are omitted for now, and possibly forever because they don't
provide anything interesting, in my humble opinion.

To me, they were added as a convenience so one can do e.g. address
calculations on a load word instruction.

<<<

}%

%{

We will need lists of registers when one makes function calls and when one
jumps to other basic blocks. Hence, we define them here

}%

reg-list : type.

reg-list/nil : reg-list.
reg-list/cons : reg -> reg-list -> reg-list.





%%%%%{ Expression Semantics }%%%%%

%{

I have decided on using small-step semantics rather than a big-step
semantics. This choice is made somewhat haphazardly on the gut
intuition that it will beneficial to have fine-grained control over
what happens.


}%

exp-step : exp -> reg -> type.

exp-step/constant : exp-step (exp/constant R) R.

exp-step/plus : exp-step (exp/plus (reg/constant N1)
			           (reg/constant N2))
		     (reg/constant N3)
		 <- nat-plus N1 N2 N3.

exp-step/lt-true : exp-step (exp/lt (reg/constant N1) (reg/constant N2)) (reg/constant 1)
		    <- nat-lt N1 N2 true.

exp-step/lt-false : exp-step (exp/lt (reg/constant N1) (reg/constant N2)) (reg/constant 0)
		     <- nat-lt N1 N2 false.


%{

Note how the step-rule only allows addition among constants. This
limitation is chosen to cope with the fact that we won't allow
arbitrarily nested expression trees.

}%







%%%%%{ Instruction syntax }%%%%%

%{

We have supposed assignment instructions are of the form

%x = exp

so a series of assignment instructions are of the form

%x1 = exp1;
%x2 = exp2;
...
%xn = expn;

Note that we use an SSA-form. This means that an assignment to x1 is
static. In other words, once x1 is assigned to in the first line, it
is impossible to reassign another value to x1. This effectively makes
x1 a constant in the following evaluation.

As an example, suppose we have the following code segment:

%x1 = 3;
%x2 = 7;
return %x1 + %x2;

This can be transformed into a functionally styled program as

let x1 = 3 in
  let x2 = 7 in
    return x1 + x2;

Which we can then transform to an equivalent program by using
lambda-abstractions for the let-body:

let 3 in
  (fn x1 => let 7 in
    (fn x2 => return x1 + x2))

The latter style is amenable to a HOAS Twelf encoding as follows. We will
define the basic operations of the language as being 'instruction's.

}%

instruction : type. %name instruction I.

instruction/let : exp -> (reg -> instruction) -> instruction.
instruction/return : reg -> instruction.
instruction/br : label -> reg-list -> instruction.
instruction/brc : reg -> label -> reg-list -> label -> reg-list -> instruction. % Conditional branch

%abbrev let = instruction/let.
%abbrev return = instruction/return.
%abbrev br = instruction/br.
%abbrev brc = instruction/brc.



%%%%%{ Basic Block Syntax }%%%%%

%{

Basic blocks are defined by a label and then a possible list of
phi-nodes. These resembles the let-construction in instructions, but
we can handle them more easily by building them into basic-blocks.

>>> NOTE
This also enforces phi-nodes to be the first thing in basic blocks by
construction
<<<

We thus set out to define basic-block headers:

}%

bb-header : type.

bb-header/phi : (reg -> bb-header) -> bb-header.
bb-header/body : instruction -> bb-header.

%abbrev phi = bb-header/phi.
%abbrev body = bb-header/body.

%{

A basic block (bb) defines a label, identifying the basic block and
then it contains a bb-header. We have already introduced the bb type family.

}%

bb/def : label -> bb-header -> bb.


%{

Here is the notational idea. A set of LLVM instructions of the form

foo:
  %p1 = phi(..,..)
  %x1 = 3;
  %x2 = %p1;
  return %x1 + %x2;

is then translated into

(bb "foo" (phi [p1]
	     (body
		(let 3 in
		   [x1] let p1 in
		     [x2] return (plus [x1] [x2])))))

(We still need to take care of the "foo" part, but the idea is there)

}%



%{

We will need lists of basic blocks. Note that if we
have the code:

label1:
  ...
  <no jump or return here>
label2:
  ...

we can always fix it by an explicit jump just before label2. Thus, it
seems ok to define lists of basic blocks as a primitive for functions.

}%


bb-list : type.

bb-list/nil : bb-list.
bb-list/cons : label -> bb -> bb-list -> bb-list.




%%%%%{ Instruction Semantics }%%%%%

%{

Static notion of instruction values

}%

instruction-value : instruction -> type.

instruction-value/return : instruction-value (instruction/return _).

%{

Evaluation of Instructions utilizes beta-reduction. As soon as we know
the value of a definition, we replace it into all uses by
substitution.

Again, we use a simple stepping relation, small-step style.

Finally, the bb-apply relation is defined here so we can refer to it in
the step/br(c) case, but we will handle it in the Basic Block Semantics section.
The idea of bb-apply is to apply a list of registers to a basic block, effectively
handling the phi-nodes in the basic block.

}%

bb-apply : reg-list -> bb-header -> instruction -> type.



instruction-step : instruction -> instruction -> type.

instruction-step/let : instruction-step (instruction/let E ([x] I x)) (I E')
			<- exp-step E E'.

instruction-step/return : instruction-step (instruction/return R) (instruction/return R).

instruction-step/br : instruction-step (instruction/br L EL) IS
		       <- bb-map L (bb/def _ BBH)
		       <- bb-apply EL BBH IS.

instruction-step/brc-t : instruction-step (instruction/brc (reg/constant 1) L EL _ _) IS
			  <- bb-map L (bb/def _ BBH)
			  <- bb-apply EL BBH IS.

instruction-step/brc-f : instruction-step (instruction/brc (reg/constant 0) _ _ L EL) IS
			  <- bb-map L (bb/def _ BBH)
			  <- bb-apply EL BBH IS.




%{

Iterated step relations

}%

instruction-steps : instruction -> instruction -> type.

instruction-steps/0 : instruction-steps I I.

instruction-steps/s : instruction-steps I I'
		       <- instruction-step I I''
		       <- instruction-steps I'' I'.



instruction-steps-to : instruction -> instruction -> type.

instruction-steps-to/do : instruction-steps-to I I'
			   <- instruction-steps I I'
			   <- instruction-value I'.




%%%%%{ Basic Block Semantics }%%%%%

%{

The following applies a list of expressions to a basic-block header, effectively
executing the basic block by substituting the values into the block and replacing
current (small-step) execution context with the contents of the BB body.

}%

bb-apply/nil : bb-apply reg-list/nil (bb-header/body I) I.

bb-apply/cons : bb-apply (reg-list/cons REG RL) (bb-header/phi ([x] PHI x)) I
		 <- bb-apply RL (PHI REG) I.



%%%%%{ Function Syntax }%%%%%


%{

The function body itself designates one basic block as the initial block by
its label. This is the block we run when asked.

}%

function-body : type.

function-body/def : label -> bb-list -> function-body.








%%%%%{ Function Semantics }%%%%%

%{

Currently, we use some very crippled function semantics. We just tell how a function
body should be executed. The plan is to wrap this knowledge later on.

First is the setup rules which allows us to set up before executing a function body. This
amount to shuffling basic blocks into the context so we can refer them later.

}%


%% Helper function for function-body-apply.
function-body-app : reg-list -> label -> bb-list -> instruction -> type.

function-body-app/nil : function-body-app REGS L bb-list/nil V
			  <- bb-map L (bb/def _ BBH)
			  <- bb-apply REGS BBH IS
			  <- instruction-steps-to IS V.

function-body-app/cons : function-body-app REGS L (bb-list/cons L' BB REST) Q
			   <- (bb-map L' BB -> function-body-app REGS L REST Q).




function-body-apply : reg-list -> function-body -> instruction -> type.

function-body-apply/do : function-body-apply REGS (function-body/def L BBL) I
			  <- function-body-app REGS L BBL I.



